     A*%%TS  SD  20200129  161407  INF1SER1    REL-V7R1M0  5770-WDS
     A* -------------------------------------------------------------
     A* SISTEMA     : Web Consulta
     A* MODULO      : COTIZACION DE POLIZAS DE HOGAR
     A* ORIGEN      : SOFTWARE LOCAL
     A* -------------------------------------------------------------
     A* FUENTE      : COW141FM
     A* DESCRIPCION : Consulta Cotizacion de Poliza
     A* AUTOR       : Sergio Luis Puentes Valladares
     A* FECHA       : 2020/01/20
     A* -------------------------------------------------------------
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A                                      PRINT
     A                                      CF12(12 'Volver')
     A          R COW141CCA
     A*%%TS  SD  20200129  151358  INF1SER1    REL-V7R1M0  5770-WDS
     A                                  1  3'COW141'
     A                                  1 13USER
     A                                  1 29'   MODULO WEB DE GAUS   '
     A                                      DSPATR(RI)
     A                                      COLOR(GRN)
     A                                  1 60DATE
     A                                      EDTCDE(Y)
     A                                  1 72TIME
     A                                  2  3'                 Consulta Cotizaci-
     A                                      on Poliza de Vida y AP             -
     A                                               '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  3  3'Prod'
     A            XXNIVT         1S 0O  3  8COLOR(BLU)
     A            XXNIVC         5Y 0O  3 10EDTCDE(Z)
     A                                      COLOR(BLU)
     A            XXPROD        40A  O  3 16COLOR(TRQ)
     A                                  3 57'N.Cot'
     A            XXNCTW         7S 0O  3 63COLOR(BLU)
     A                                  4  3'Rama'
     A            XXRAMA         2Y 0O  4  8EDTCDE(Z)
     A                                      COLOR(BLU)
     A            XXDRAM        30A  O  4 11COLOR(TRQ)
     A                                  4 42'Art'
     A            XXARCD         6Y 0O  4 46COLOR(BLU)
     A                                      EDTCDE(Z)
     A            XXARNO        23A  O  4 53COLOR(TRQ)
     A          R COW141CS1                 SFL
     A*%%TS  SD  20200128  094053  INF1SER1    REL-V7R1M0  5770-WDS
     A            X1EMPR         1A  H
     A            X1SUCU         2A  H
     A            X1RAMA         2S 0H
     A            X1ARSE         2S 0H
     A            X1NIVT         1S 0H
     A            X1NIVC         5S 0H
     A            X1OPCI         1Y 0B  9 10DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                      COLOR(BLU)
     A            X1ACTI         5Y 0O  9 14COLOR(PNK)
     A                                      EDTCDE(Z)
     A            X1SECU         2S 0O  9 21COLOR(PNK)
     A            X1CATE         2S 0O  9 25COLOR(PNK)
     A            X1CANT         2S 0O  9 29COLOR(PNK)
     A            X1DACT        40A  O  9 36COLOR(TRQ)
     A          R COW141CC1                 SFLCTL(COW141CS1)
     A*%%TS  SD  20200129  091920  INF1SER1    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0012)
     A  30                                  SFLDSP
     A  30                                  SFLDSPCTL
     A  31                                  SFLCLR
     A  32                                  SFLEND(*MORE)
     A                                      OVERLAY
     A            NRSFL          4S 0H      SFLRCDNBR(CURSOR)
     A                                  5  8'5=Consultar'
     A                                      COLOR(BLU)
     A                                  6  9'OP'
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  6 14'Acti.'
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  6 20'Sec'
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  6 24'Cat'
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  6 28'Cant'
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                  6 36'Actividad                         -
     A                                            '
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A            XXACTI         5Y 0B  7 14EDTCDE(Z)
     A                                      COLOR(BLU)
     A                                      CHECK(RZ)
     A          R COW141CS2                 SFL
     A*%%TS  SD  20200129  141951  INF1SER1    REL-V7R1M0  5770-WDS
     A            X1EMPR         1A  H
     A            X1SUCU         2A  H
     A            X1NIVT         1S 0H
     A            X1NIVC         5S 0H
     A            X1NCTW         7S 0H
     A            X1RAMA         2S 0H
     A            X1ARSE         2S 0H
     A            X1OPCI         1Y 0B 16  6DSPATR(HI)
     A                                      EDTCDE(Z)
     A                                      COLOR(BLU)
     A  40                                  DSPATR(ND)
     A  40                                  DSPATR(PR)
     A            X1POCO         6Y 0O 16  9COLOR(BLU)
     A  40                                  DSPATR(ND)
     A                                      EDTWRD('    0 ')
     A            X1PACO         3Y 0O 16 16
     A  40                                  DSPATR(ND)
     A                                      COLOR(BLU)
     A                                      EDTWRD(' 0 ')
     A            X1DPAC        18A  O 16 20COLOR(TRQ)
     A  40                                  DSPATR(ND)
     A            X1NOMB        40A  O 16 39
     A  40                                  DSPATR(ND)
     A                                      COLOR(PNK)
     A          R COW141CC2                 SFLCTL(COW141CS2)
     A*%%TS  SD  20200129  161407  INF1SER1    REL-V7R1M0  5770-WDS
     A                                      SFLSIZ(0100)
     A                                      SFLPAG(0004)
     A  30                                  SFLDSP
     A  30                                  SFLDSPCTL
     A  34                                  SFLCLR
     A  35                                  SFLEND(*MORE)
     A                                      OVERLAY
     A            RRSFL          4S 0H      SFLRCDNBR(CURSOR)
     A                                  5  5'Actividad'
     A            XXACTI         5Y 0O  5 15COLOR(BLU)
     A                                      EDTWRD('   0 ')
     A                                  5 25'Secuencia'
     A            XXSECU         2S 0O  5 35COLOR(BLU)
     A                                  5 40'Categoria'
     A            XXCATE         2S 0O  5 50COLOR(BLU)
     A                                  5 54'Cantidad'
     A            XXCANT         2S 0O  5 63COLOR(BLU)
     A                                  6  5'Suma Aseg.Total'
     A            XXSUAS        13Y 0O  6 25EDTWRD(' .   .   . 0 .   ')
     A                                      COLOR(BLU)
     A                                  6 43'Prima Tarifa'
     A            XXPRIM        15Y 2O  6 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  7  5'Sellado Riesgo'
     A            XXSERI        15Y 2O  7 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  7 43'Sellado Empresa'
     A            XXSEEM        15Y 2O  7 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  8  5'Imptos Internos'
     A            XXIMPI        15Y 2O  8 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  8 43'Serv.Sociales'
     A            XXSERS        15Y 2O  8 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  9  5'Tasa Sup.Seg.Nac'
     A            XXTSSN        15Y 2O  9 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                  9 43'Impto IVA'
     A            XXIPR1        15Y 2O  9 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 10  5'IVA Resp.No Insc'
     A            XXIPR4        15Y 2O 10 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 10 43'IVA Imp.Percep'
     A            XXIPR3        15Y 2O 10 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 11  5'Comp.Premio 6'
     A            XXIPR6        15Y 2O 11 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 11 43'Comp.Premio 7'
     A            XXIPR7        15Y 2O 11 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 12  5'Comp.Premio 8'
     A            XXIPR8        15Y 2O 12 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 12 43'Comp.Premio 9'
     A            XXIPR9        15Y 2O 12 59EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 13  5'Premio Bruto '
     A            XXPREM        15Y 2O 13 22EDTWRD(' .   .   .   . 0 ,  ')
     A                                      COLOR(BLU)
     A                                 14  5'OP 1=Mas datos   2=Cobertura'
     A                                      COLOR(RED)
     A                                 15  5'OP '
     A                                      COLOR(BLU)
     A                                      DSPATR(RI)
     A                                      DSPATR(UL)
     A                                 15  9' Comp '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 16'Parentesco            '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 39'Nombre                            -
     A                                            '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A          R COW141CP1
     A*%%TS  SD  20200122  083603  INF1SER1    REL-V7R1M0  5770-WDS
     A                                      OVERLAY
     A            WMSG          78A  O 22  2DSPATR(BL)
     A                                      COLOR(RED)
     A                                 23  2'HDI SEGUROS S.A.                  -
     A                                                     HDI SEGUROS S.A.    -
     A                                               '
     A                                      DSPATR(UL)
     A                                 24 70'F12=Volver'
     A                                      COLOR(BLU)
